plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'fabric-loom'
}

base {
    archivesName = "${mod_name}-fabric-${minecraft_version}"
}

repositories {
    maven {
        url = "https://maven.terraformersmc.com/releases/"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()

    modImplementation "net.fabricmc:fabric-loader:${fabric_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}+${minecraft_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":common")

//    modRuntimeOnly("curse.maven:catalogue-fabric-667377:${fabric_catalogue_version}")
//    modRuntimeOnly("curse.maven:configured-fabric-667378:${fabric_configured_v}")
    modRuntimeOnly("mezz.jei:jei-${minecraft_version}-fabric:${jei_version}")
//    modRuntimeOnly("me.shedaniel:RoughlyEnoughItems-fabric:${rei_version}")

    modImplementation("com.terraformersmc:modmenu:${modmenu_version}")
    modImplementation("curse.maven:cake-chomps-307943:${fabric_cake_chomps_version}")
    modImplementation("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}")
    modImplementation("dev.architectury:architectury-fabric:${architectury_version}")
    modImplementation("fuzs.forgeconfigapiport:forgeconfigapiport-fabric:${config_port_version}")
    modImplementation("maven.modrinth:jade:${fabric_jade_version}")

    modCompileOnlyApi("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}")
    modCompileOnlyApi("mezz.jei:jei-${minecraft_version}-fabric-api:${jei_version}")

    modCompileOnly("me.shedaniel:RoughlyEnoughItems-api-fabric:${rei_version}")
    modCompileOnly("me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${rei_version}")

    implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:0.3.5"))
}

loom {
    accessWidenerPath.set(project(":common").file("src/main/resources/${mod_id}.accesswidener"))

    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }

    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
        data {
            inherit client
            setConfigName("Fabric Data")
            ideConfigGenerated(true)
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=${mod_id}"
            runDir("build/datagen")
        }
    }
}

sourceSets.main.resources { srcDirs += ["src/main/generated"] }

processResources {
    from project(":common").sourceSets.main.resources

    inputs.property("version", version)

    filesMatching("fabric.mod.json") {
        expand("version": version)
    }
}

tasks.withType(JavaCompile).configureEach {
    source(project(":common").sourceSets.main.allSource)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId base.archivesName.get()
            from components.java
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
