buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}
apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = "${mod_v}"
group = "${mod_group}"
archivesBaseName = "${mod_name}-forge-${mc_v}"

minecraft {
    mappings channel: 'official', version: "${mc_v}"
    
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Client'
            mods {
                modClientRun {
                    source sourceSets.main
                    source project(":Common").sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Server'
            mods {
                modServerRun {
                    source sourceSets.main
                    source project(":Common").sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            args '--mod', "${mod_id}", '--all', '--output', file("src/generated/resources/"), '--existing', project(":Common").file("src/main/resources/")
            taskName 'Data'
            mods {
                modDataRun {
                    source sourceSets.main
                    source project(":Common").sourceSets.main
                }
            }
        }
    }
}

sourceSets {
    main {
        resources {
            srcDir "src/generated/resources"
            excludes.add("jmcCommon.accesswidener")
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_v}-${forge_v}"
    compileOnly project(":Common")

    implementation fg.deobf("curse.maven:jei-238222:${forge_jei_v}")
    implementation fg.deobf("curse.maven:catalogue-459701:${catalogue_v}")
    implementation fg.deobf("curse.maven:configured-457570:${configured_v}")
    implementation fg.deobf("curse.maven:cake-chomps-307943:${forge_cake_chomps_v}")
}

jar {
    excludes.add("einstein/${mod_id}/data/generators")
}

processResources {
    from project(":Common").sourceSets.main.resources
    inputs.property "version", "${mod_v}"

    filesMatching("META-INF/mods.toml") {
        expand "version": "${mod_v}"
    }
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
